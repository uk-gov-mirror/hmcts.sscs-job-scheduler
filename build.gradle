plugins {
  id 'java'
  id 'java-library'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.owasp.dependencycheck' version '12.1.1'
  id 'com.github.ben-manes.versions' version '0.52.0'
  id 'org.sonarqube' version '6.2.0.5505'
  id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
  dependsOn javadoc
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

compileJava {
  options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
  options.compilerArgs << "-Xlint:unchecked"
}

compileIntegrationTestJava {
  options.compilerArgs << "-Xlint:unchecked"
}

tasks.register('integration', Test) {
  description = "Runs Integration Tests"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

checkstyle {
  toolVersion = '10.12.4'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "7.13.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
  reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacoco {
  toolVersion = "0.8.13"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks.named('sonarqube') {
  dependsOn test, integration, jacocoTestReport
}

sonarqube {
  properties {
    property "sonar.projectName", "SSCS - SSCS job scheduler"
    property "sonar.projectKey", "SSCSJS"
    property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation}"
    property "sonar.exclusions", "**/src/main/java/uk/gov/hmcts/reform/sscs/jobscheduler/config/**"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

tasks.withType(Copy).configureEach { duplicatesStrategy DuplicatesStrategy.WARN }

tasks.withType(Test).configureEach {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

repositories {
  mavenCentral()
  mavenLocal()
  mavenCentral()

  maven { url 'https://repo.spring.io/libs-milestone' }
  maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }

  // jitpack was last resort but replaced bintray
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }

}

artifacts {
  archives sourcesJar
  archives javadocJar
}

publishing {
  publications {
    Main(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId project.group
      artifactId 'sscs-job-scheduler'
      version project.version
    }
  }

  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
}

configurations.configureEach {
  resolutionStrategy {
    eachDependency { DependencyResolveDetails details ->
      if (details.requested.group in ['com.fasterxml.jackson.core', 'com.fasterxml.jackson.module', 'com.fasterxml.jackson.datatype']) {
        details.useVersion '2.19.0'
      }
    }
  }
}

dependencyManagement {
  dependencies {

  }
}

def versions = [
        springBoot   : '3.4.5',
]

dependencies {
  implementation group: 'org.quartz-scheduler', name: 'quartz', version: '2.5.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'
  implementation group: 'com.github.hmcts', name: 'reform-api-standards', version: '0.5.2', {
    exclude group: 'com.google.guava', module: 'guava'
  }

  implementation group: 'jakarta.inject', name: 'jakarta.inject-api', version: '2.0.1'
  implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot
  implementation group: 'org.springframework', name: 'spring-context-support', version: '6.2.7'

  implementation group: 'org.springframework', name: 'spring-tx', version: '6.2.7'

  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '3.0.1'
  testImplementation group: 'org.json', name: 'json', version: '20250517'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.18.0'
  testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.18.0'
  testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.12.2'
  testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.12.2'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.12.2'

  integrationTestImplementation(sourceSets.test.output)
  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
}

